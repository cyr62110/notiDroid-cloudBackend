apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'notidroid-cloudBackend'
    version = '0.2.0-SNAPSHOT'
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/libs-snapshot" }
}

ext {
    activeMQVersion = "5.10.0"
    springVersion = "4.0.3.RELEASE"
    embeddedTomcatVersion = "8.0.8"
}

dependencies {
    //We will run our project on a servlet container
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    //Which will be an embedded tomcat
    compile "org.apache.tomcat:tomcat-catalina:$embeddedTomcatVersion"
    compile "org.apache.tomcat:tomcat-jasper:$embeddedTomcatVersion"

    //We use the Spring Framework as the core of our service
    compile "org.springframework:spring-core:$springVersion"

    //Spring MVC for the REST interface and the admin UI
    compile "org.springframework:spring-webmvc:$springVersion"

    //We use Jackson2 to serialize our REST resources
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1'

    //Push message will also be serialized using Jackson but in BSON
    compile 'de.undercouch:bson4jackson:2.4.0'

    //We use Thymeleaf for our admin side. The client will be developed using Angular.Dart
    //We add also a dialect that will allow us to build master page with Thymeleaf
    compile 'org.thymeleaf:thymeleaf-spring4:2.1.3.RELEASE'
    compile 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:1.2.4'

    //We use an embedded Neo4J for the storage of user, applications, relations
    compile 'org.neo4j:neo4j:2.0.+'

    //Storage of notification, images are delegated to a MongoDB and its GridFS
    compile 'org.springframework.data:spring-data-mongodb:1.5.2.RELEASE'

    //We use ActiveMQ as base of our push-notification framework.
    //And we use Spring-JMS to communicate with our ActiveMQ instances.
    compile "org.springframework:spring-jms:$springVersion"
    compile "org.apache.activemq:activemq-client:$activeMQVersion"
    runtime "org.apache.activemq:activemq-jaas:$activeMQVersion"

    //With spring security for authentication on both side
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.0.2.RELEASE'
    compile 'org.springframework.security:spring-security-core:3.2.4.RELEASE'
    compile 'org.springframework.security:spring-security-config:3.2.4.RELEASE'

    //We will also use bootstrap 3 to make a nice UI for the admin side
    compile 'org.webjars:jquery:1.11.0'
    compile 'org.webjars:bootstrap:3.1.1'

    //API and Implementation of the Java Validation API for validating data coming from client implementations.
    runtime 'javax.validation:validation-api:1.0.0.GA'
    runtime 'org.apache.bval:bval-core:0.5'
    runtime 'org.apache.bval:bval-jsr303:0.5'

    //Spring data jpa to have spring data cool repository
    compile 'org.springframework.data:spring-data-neo4j-rest:3.1.0.RELEASE'

    //Runtime dependencies for Spring Data Neo4J
    //Javax WS API
    runtime 'javax.ws.rs:javax.ws.rs-api:2.0'
    //Jersey implementation
    runtime 'com.sun.jersey:jersey-bundle:1.18.1'

    //Commons-Lang3 utility library cause im lazy
    compile 'org.apache.commons:commons-lang3:3.3.2'

    //Commons-Validator to validate email addresses and more
    compile 'commons-validator:commons-validator:1.4.0'

    //Commons-IO for manipulating files
    compile 'commons-io:commons-io:2.4'

    //Some librairies used for logging purpose
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-rc1'
    compile 'org.apache.logging.log4j:log4j:2.0-rc1'
    compile 'org.apache.logging.log4j:log4j-core:2.0-rc1'

    //Do not forget to test our code with JUnit
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile 'junit:junit:4.11'

    compile project(':client-api')
    compile project(':private-api')
    compile project(':re-mapper')

}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}